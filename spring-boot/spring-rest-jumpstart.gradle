def local_spring_profile = project.properties['localSpringProfile'] ?: "local"
def gradle_templates_source = project.properties['gradleTemplatesSource'] ?: "https://raw.githubusercontent.com/JosephLogue3/gradle-build-templates/master/gradle-common"
def test_containers_enabled = project.properties['testContainersEnabled'] ?: "false"
def test_containers_reusable = project.properties['testContainersReusable'] ?: "false"
def configured_containers = project.properties['configuredContainers'] ?: ""
def testContiner_application_initializer_class = project.properties['testContinerApplicationInitializerClass'] ?: ""

// local testing
// def gradle_templates_source = "../../gradle-common"
plugins{
  id 'java'
  id 'groovy'
  id 'scala'
  id 'idea'
  id 'maven-publish'
  id 'groovy'
  id 'org.springframework.boot'
  id 'io.spring.dependency-management'
  id "com.github.ben-manes.versions" version "0.42.0"
}


apply from: "${gradle_templates_source}/common-utils-tasks.gradle"
apply from: "${gradle_templates_source}/git-common-functions.gradle"
apply from: "${gradle_templates_source}/git-semantic-release.gradle"
apply from: "${gradle_templates_source}/app-monitoring-configuration.gradle"
apply from: "${gradle_templates_source}/java-code-quality.gradle"
apply from: "${gradle_templates_source}/integration-tests.gradle"
apply from: "${gradle_templates_source}/performance-tests.gradle"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11



bootJar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Application-Description': project.description,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date().format("EEE MMM dd HH:mm:ss zzz yyyy "),
                'Built-JDK': System.getProperty('java.version'),
                'Created-By': 'Gradle',
                'Git-Commit': getRevision(),
                'Git-Branch': getBranch(),
                'Source-Compatibility': sourceCompatibility,
                'Target-Compatibility': targetCompatibility
    }
}

springBoot {
    buildInfo {
        properties {
            additional = [
                    'description': project.description,
                    'Built-By': System.getProperty('user.name'),
                    'Git-Commit': getRevision(),
                    'Git-Branch': getBranch(),
                    'Source-Compatibility': sourceCompatibility,
                    'Target-Compatibility': targetCompatibility
            ]
        }
    }
}

bootRun {
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "${local_spring_profile}"
    args "application.testcontainers.enable=${test_containers_enabled}"
    args "application.testcontainers.reusable=${test_containers_reusable}"
    args "application.testcontainers.containers=${configured_containers}"
    args "application.testcontainers.initializer=${testContiner_application_initializer_class}"
}

task processDocker(type: Copy) {
    description = 'Copies the Dockerfile and replaces tokens'
    project.ext.SPRING_PROFILES_ACTIVE = '${SPRING_PROFILES_ACTIVE}'
    project.ext.GIT_COMMIT = getRevision()
    fileMode = 0755
    from('src/main/docker/')
    into('build/libs/')
    expand(project.properties)
}
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}
tasks.named("dependencyUpdates").configure {
    checkConstraints = true
    checkBuildEnvironmentConstraints = true
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

build.dependsOn processDocker